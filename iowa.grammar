Program        → Declaration* EOF ;

Declaration    → StaticDecl 
               | Statement ;

Statement       → ExpressionStatement
                | Assignment
                | If
                | Block ;

Block           → "{" Statement* "}" ;
StaticDecl      → Type Identifier "=" Expression ";" ;
Assignment      → Identifier "=" Expression ";" ;

Expression      → Equality ;
Equality        → Comparison (EqualityOperator Comparison)* ;
Comparison      → Term (ComparisonOperator Term)* ;
Term            → Factor (("+" | "-") Factor)* ;
Factor          → Primary (("*" | "/") Primary)* ;
Primary         → Identifier | Literal | "(" Expression ")" ;

ExpressionStatement → Expression ";" ;
If              → "if" "(" Expression ")" Block ("else" Block)? ;

Literal         → Number | String ;
Identifier      → [a-zA-Z_][a-zA-Z0-9_]* ;
Number          → [0-9]+ ;
String          → "\"" .*? "\"" ;

EqualityOperator        → "==" | "!=" ;
ComparisonOperator      → ">" | "<" ;
